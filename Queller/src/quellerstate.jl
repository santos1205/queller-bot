################################################################################

struct ActiveDie
	die::Die.Face
	use_as::Die.Face
	use_modt::Bool
	use_ring::Bool

	used::Bool
end

ActiveDie(die, use_as, use_modt, use_ring) = ActiveDie(die, use_as, use_modt, use_ring, false)
use_active_die(d) = ActiveDie(d.die, d.use_as, d.use_modt, d.use_ring, true)

article(d::ActiveDie) = Die.article(d.die)

function Base.string(d::ActiveDie)
	d.die == d.use_as && return string(d.die)

	text = "$(string(d.die)) como $(string(d.use_as))"
	d.use_modt && return text*" usando a habilidade Mensageiro da Torre Negra"
	d.use_ring && return text*" usando um Anel Élfico"
	return text
end

function get_active_die(die, available_dice, strategy, may_use_modt, may_use_ring)
	die in available_dice && return ActiveDie(die,die,false,false)

	(Die.ArmyMuster in available_dice) && (die == Die.Army || die == Die.Muster) && return ActiveDie(Die.ArmyMuster,die,false,false)
	(Die.Muster in available_dice) && (die == Die.Army && may_use_modt) && return ActiveDie(Die.Muster,die,true,false)

	strategy == Strategy.Military && (discardable_dice = [Die.Character, Die.Event])
	strategy == Strategy.Corruption && (discardable_dice = [Die.Army, Die.Muster, Die.ArmyMuster, Die.Event])

	discardable_dice = intersect(available_dice, discardable_dice)
	may_use_ring && !isempty(discardable_dice) && return ActiveDie(rand(discardable_dice),die,false,true)

	return nothing
end


################################################################################

mutable struct QuellerState
	strategy::Strategy.Choice

	active_die::Union{ActiveDie,Nothing}
	available_dice::Vector{Die.Face}

	ring_available::Bool
	modt_available::Bool
end

function QuellerState(strategy::Strategy.Choice, available_dice = Vector{Die.Face}() )
	active_die = nothing

	ring_available = false
	modt_available = false

	QuellerState(strategy,active_die,available_dice,ring_available,modt_available)
end


################################################################################

mutable struct CheckStrategy <: StateInteractionNode
	id::String
	strategy::Strategy.Choice
	n_true::Node
	n_false::Node

	CheckStrategy(strategy) = (obj = new(); obj.strategy = StrategyChoice(strategy); obj)
end
getnext!(n::CheckStrategy, state) = (state.strategy == n.strategy ? n.n_true : n.n_false)
setnext!(n::CheckStrategy, name::Symbol, next::Node) = setfield!(n,name,next)

getmsg(n::CheckStrategy) = "A estratégia $(string(n.strategy)) é usada."
getmsg(n::CheckStrategy, state) = "$(getmsg(n)): $(state.strategy == n.strategy)"


################################################################################

mutable struct SetStrategy <: StateInteractionNode
	id::String
	strategy::Strategy.Choice
	next::Node

	SetStrategy(strategy) = (obj = new(); obj.strategy = StrategyChoice(strategy); obj)
end
setnext!(n::SetStrategy, next::Node) = (n.next = next)
getnext!(n::SetStrategy, state) = (state.strategy = n.strategy; n.next)

getmsg(n::SetStrategy) = "Definir estratégia do Queller para $(string(n.strategy))."


################################################################################

mutable struct CheckActiveDie <: StateInteractionNode
	id::String
	die::Die.Face
	n_true::Node
	n_false::Node

	CheckActiveDie(die) = (obj = new(); obj.die = DieFace(die); obj)
end
setnext!(n::CheckActiveDie, name::Symbol, next::Node) = setfield!(n,name,next)
getnext!(n::CheckActiveDie, state) = (state.active_die.use_as == n.die ? n.n_true : n.n_false)
getmsg(n::CheckActiveDie) = "O dado ativo é $(Die.article(n.die)) $(string(n.die))"
getmsg(n::CheckActiveDie, state) = "$(getmsg(n)): $(state.active_die.use_as == n.die)"



################################################################################

mutable struct SetActiveDie <: StateInteractionNode
	id::String
	next::Node
	no_die::Node

	die::Die.Face
	may_use_ring::Bool

	SetActiveDie(die; may_use_ring=false) = (obj = new(); obj.die = DieFace(die); obj.may_use_ring = may_use_ring; obj)
end
setnext!(n::SetActiveDie, name::Symbol, next::Node) = setfield!(n,name,next)

get_active_die(n::SetActiveDie,state) =
	get_active_die(n.die, state.available_dice, state.strategy, state.modt_available, state.ring_available && n.may_use_ring)

function getnext!(n::SetActiveDie,state)
	state.active_die = get_active_die(n, state)
	return isnothing(state.active_die) ? n.no_die : n.next
end

getmsg(n::SetActiveDie, state) = "O dado ativo está definido como: $(string(get_active_die(n,state)))"

function getmsg(n::SetActiveDie)
	prio_list = [string(n.die)]
	if n.die == Die.Army
		push!(prio_list, "$(string(Die.ArmyMuster)) as $(string(Die.Army))")
		push!(prio_list, "$(string(Die.Muster)) and Messenger of the Dark Tower as $(string(Die.Army))")
	elseif n.die == Die.Muster
		push!(prio_list, "$(string(Die.ArmyMuster)) as a $(string(Die.Muster))")
	end

	n.may_use_ring && push!(prio_list, "Um dado aleatório não-*preferencial* e um Anel Élfico como $(string(n.die))")

	text = """
	Defina o primeiro dado correspondente como o Dado Ativo:

	"""
	for (i, d) in enumerate(prio_list)
		text *= "$(i). $(d)\n"
	end
	return text
end


################################################################################

mutable struct UseActiveDie <: StateInteractionNode
	id::String
	next::Node

	UseActiveDie() = new()
end
setnext!(n::UseActiveDie, next::Node) = (n.next = next)
function getnext!(n::UseActiveDie, state)
	isnothing(state.active_die) && error("Nenhum dado ativo foi definido antes de ser usado.")

	if !state.active_die.used
		state.active_die = use_active_die(state.active_die)
		i = findfirst(==(state.active_die.die), state.available_dice)
		state.available_dice = deleteat!(state.available_dice, i)
	end
	return n.next
end

getmsg(n::UseActiveDie) = "Usar o Dado Ativo para:"
function getmsg(n::UseActiveDie, state)
	die = state.active_die
	return "Usar $(article(die)) $(string(die)) para:"
end


################################################################################

mutable struct SetRingAvailable <: StateInteractionNode
	id::String
	next::Node
	ring_available::Bool

	SetRingAvailable(ring_available) = (obj = new(); obj.ring_available = ring_available; obj)
end
setnext!(n::SetRingAvailable, next::Node) = (n.next = next)
getnext!(n::SetRingAvailable, state) = (state.ring_available = n.ring_available; n.next)
getmsg(n::SetRingAvailable) = n.ring_available ? "Definir um Anel Élfico como disponível." : "Definir um Anel Élfico como não disponível."


################################################################################

mutable struct SetMoDTAvailable <: StateInteractionNode
	id::String
	next::Node
	modt_available::Bool

	SetMoDTAvailable(modt_available) = (obj = new(); obj.modt_available = modt_available; obj)
end
setnext!(n::SetMoDTAvailable, next::Node) = (n.next = next)
getnext!(n::SetMoDTAvailable, state) = (state.modt_available = n.modt_available; n.next)
getmsg(n::SetMoDTAvailable) = n.modt_available ? "Definir 'Mensageiro da Torre Negra' como disponível." : "Definir 'Mensageiro da Torre Negra' como não disponível."


################################################################################

mutable struct GetAvailableDice <: InteractiveNode
	id::String
	next::Node
	action::String

	GetAvailableDice(action = """
					 Insira os dados de ação disponíveis (sem contar dados reservados para uso posterior).
					 """) = (obj = new(); obj.action = strip(action); obj)
end
setnext!(n::GetAvailableDice, next::Node) = (n.next = next)
getopt(n::GetAvailableDice) = [CMD.Dice()]
getnext!(n::GetAvailableDice, state, opt::CMD.Dice) = (state.available_dice = opt.dice; n.next)

function getmsg(n::GetAvailableDice)
	diefaces = instances(Die.Face)
	legends = Die.char.(diefaces).*": ".*string.(diefaces)

	return """
	$(n.action)

	$(strvec2str(legends))
	"""
end


